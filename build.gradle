import org.apache.tools.ant.filters.ReplaceTokens

import java.text.SimpleDateFormat

buildscript {
    ext.kotlin_version = '1.0.1-2'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        // lookup the most recent version here: http://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-gradle-plugin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath "gradle.plugin.edu.sc.seis:macAppBundle:2.1.1"
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}

plugins {
    id 'jacoco'
    // see: https://github.com/kt3k/coveralls-gradle-plugin
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "http://jaspersoft.artifactoryonline.com/jaspersoft/third-party-ce-artifacts/"
    }
}

// =====================================================================================================================
// MISC
// =====================================================================================================================

ext {
    // projectVersion = '1.0.0-SNAPSHOT'
    myMainClassName = 'at.cpickl.gadsu.Gadsu'
    myAppName = 'Gadsu'
}

// version = projectVersion

apply plugin: "kotlin"

defaultTasks 'run'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

// =====================================================================================================================
// DEPENDENCIES
// =====================================================================================================================

configurations.all {
    exclude group: "commons-logging", module: "commons-logging"
}

dependencies {

    // MISC
    // -------------------------------------------------------------------------
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'com.google.inject:guice:4.0'
    compile 'com.google.guava:guava:18.0' // guice comes with 16.0.1
    compile 'com.google.inject.extensions:guice-assistedinject:4.0'
    compile 'joda-time:joda-time:2.8.1'
    compile 'commons-cli:commons-cli:1.3'

    // DB
    // -------------------------------------------------------------------------
    compile 'org.hsqldb:hsqldb:2.3.3'
    compile 'org.springframework:spring-jdbc:4.2.5.RELEASE'
    compile 'org.flywaydb:flyway-core:4.0'

    // VIEW
    // -------------------------------------------------------------------------
    // compile 'org.java.net.substance:substance:6.0'
    compile 'org.jdatepicker:jdatepicker:1.3.4'

    // jasper PDF
    compile 'net.sf.jasperreports:jasperreports:6.1.0'
    compile 'com.lowagie:itext:2.1.7'

    // LOG
    // -------------------------------------------------------------------------
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'ch.qos.logback:logback-core:1.1.3'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.slf4j:jcl-over-slf4j:1.7.21' // everyone who uses apaches common logging should use this instead

    // TEST
    // -------------------------------------------------------------------------
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'// testng comes with hamcrest-core 1.1 via junit; must be ABOVE
    testCompile 'org.exparity:hamcrest-bean:1.0.11'
    testCompile('org.testng:testng:6.9.6')
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile(group: 'org.uispec4j', name: 'uispec4j', version: '2.5-RC1', classifier: 'jdk8')
    testCompile 'com.google.inject.extensions:guice-testlib:4.0'
}

// =====================================================================================================================
// PACKAGING
// =====================================================================================================================

// https://github.com/musketyr/gradle-fatjar-plugin
// ./gradlew fatJar
apply plugin: 'eu.appsatori.fatjar'

task loadProjectVersionNumber {
    Properties props = new Properties()
    File propsFile = new File("version.properties")
    props.load(propsFile.newDataInputStream())
    version = props['version']
    /*
    Integer nextbuildnum = ( ((props.getProperty('artifactBuildNumber')) as BigDecimal) + 1 )
    props.setProperty('artifactBuildNumber', nextbuildnum.toString())
    props.store(propsFile.newWriter(), null)
    props.load(propsFile.newDataInputStream())
    */

}
processResources.dependsOn loadProjectVersionNumber

processResources {
    from('src/main/resources') {
        include '**/metainf.properties'
        filter ReplaceTokens, tokens: [
                "application.version": version,
                "built.date"         : new SimpleDateFormat("dd.MM.yyyy HH:mm:ss").format(new Date())
        ]
    }
}


fatJar {
    manifest {
        // splashscreen-image manifest entry must be in "main JAR" file
        // java -jar is going to ignore -classpath option
        //   => either fat-jar, or reference other lib-jars in manifest
        attributes 'Implementation-Title': 'Gadsu',
                'Implementation-Version': version,
                'Main-Class': myMainClassName, // actually not necessary
                'SplashScreen-Image': "gadsu/splashscreen.jpg",
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
    }
    /*
    dependsOn configurations.runtime
    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    */
}

fatJarPrepareFiles {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
}

// =====================================================================================================================
// PACKAGING MAC
// =====================================================================================================================

// $ gradle createApp -Dgadsu.enableMacBundle=true
// $ gradle createDmg -Dgadsu.enableMacBundle=true

// travis lacks hdiutil binary, so only enabled by explicit sysproperty
if (System.properties.getProperty("gadsu.enableMacBundle") == "true") {
    println "enabling mac bundle"
    // NOPE!
    //apply plugin: 'application'
    //mainClassName = myMainClassName
    // applicationDefaultJvmArgs = ["-Dfoo=bar"]

    // https://github.com/crotwell/gradle-macappbundle
    // https://github.com/crotwell/gradle-macappbundle/wiki/Intro
    apply plugin: 'edu.sc.seis.macAppBundle'

    macAppBundle {
        appName = myAppName
        dmgName = myAppName + "-" + version

        mainClassName = myMainClassName
        icon = "src/main/buildMac/logo.icns"
        jvmVersion = "1.8+"

        javaProperties.put("apple.laf.useScreenMenuBar", "true")
        javaProperties.put("gadsu.isMacApp", "true")

        // http://alvinalexander.com/apple/mac/java-mac-native-look/Putting_your_application_na.shtml
        // NOPE javaProperties.put("com.apple.mrj.application.apple.menu.about.name", myAppName)

        javaExtras.put("-splash:\$APP_ROOT/Contents/Resources/splashscreen.jpg", null)

        // list of all options:
        // https://github.com/crotwell/gradle-macappbundle/blob/master/src/main/groovy/edu/sc/seis/macAppBundle/MacAppBundlePluginExtension.groovy
    }

    task copySplashscreenForMacApp(type: Copy) {
        //    println "Copying splash screen for mac app ..."
        from 'src/main/resources/gadsu/splashscreen.jpg'
        into 'build/macApp/' + myAppName + '.app/Contents/Resources'
    }
    createApp.dependsOn copySplashscreenForMacApp
}

// =====================================================================================================================
// TEST
// =====================================================================================================================

// run all tests:
// >> gradle test testIt testUi

// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.testing.Test.html
// http://stackoverflow.com/questions/21009125/including-not-excluding-groups-in-testng-and-gradle
// suite in gradle: http://stackoverflow.com/questions/25774964/gradle-testng-how-to-specify-testng-include-group-in-build-gradle-while-using
test {
    testLogging.showStandardStreams = false
    //minHeapSize = "128m"
    //maxHeapSize = "512m"

    reports.junitXml.destination = file("${buildDir}/test-results/unit")
    useTestNG() {
        // https://docs.gradle.org/current/groovydoc/org/gradle/api/tasks/testing/testng/TestNGOptions.html
        excludeGroups 'uiTest'

        //parallel = 'classes'
        //threadCount = 1 // !!!! only increase thread count for real unit tests (will break HSQLDB enabled tests!!!)
    }

    testLogging {
//        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        events "skipped", "failed", "standardError"
        exceptionFormat "full" // default is "short"
    }
    jacoco {
        append = true
//        destinationFile = file("$buildDir/jacoco/jacocoTest-unit.exec")
//        classDumpFile = file("$buildDir/jacoco/classpathdumps-unit")
    }
//    beforeTest { descriptor ->
//        logger.lifecycle("RUN: " + descriptor)
//    }
//    onOutput { descriptor, event ->
//        logger.lifecycle("[OUT] " + descriptor + " >>> " + event.message)
//    }
}

/*
task testIt(type: Test) {
    useTestNG() {
        excludeGroups ''
        includeGroups 'it', 'db'
    }
    reports.html.destination = file("${reporting.baseDir}/testIt")
    reports.junitXml.destination = file("${buildDir}/test-results/it")
}
*/

// check.dependsOn testUi

task testUi(type: Test) {
    testLogging.showStandardStreams = false
    // systemProperty 'uispec4j.test.library', 'testng'

    reports.html.destination = file("${reporting.baseDir}/testUi")
    reports.junitXml.destination = file("${buildDir}/test-results/ui")

    // nice to not override: systemProperties.BROWSER = System.getProperty('BROWSER', 'internetExplorer')
    useTestNG() {
        includeGroups 'uiTest'
        // suites 'src/main/resources/testng.xml'
    }
    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/test.exec") // same as unit tests :)
//        destinationFile = file("$buildDir/jacoco/jacocoTest-ui.exec")
//        classDumpFile = file("$buildDir/jacoco/classpathdumps-ui")
    }
}

//apply plugin: "jacoco"

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}
